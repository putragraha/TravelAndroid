apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.neptuunia.travel"

        minSdkVersion 21
        targetSdkVersion 29

        versionCode 1
        versionName "1.0"

        manifestPlaceholders = [mapsApiKey:project.properties['mapsApiKey']]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "account"
    productFlavors {
        driver {
            dimension "account"
            applicationIdSuffix '.driver'
            versionNameSuffix '-driver'
            manifestPlaceholders = [
                    appIcon         : "@mipmap/ic_driver_icon",
                    appRoundIcon    : "@mipmap/ic_driver_icon_round"
            ]
        }

        user {
            dimension "account"
            applicationIdSuffix '.user'
            versionNameSuffix '-user'
            manifestPlaceholders = [
                    appIcon         : "@mipmap/ic_user_icon",
                    appRoundIcon    : "@mipmap/ic_user_icon_round"
            ]
        }
    }

    applicationVariants.all { variant ->
        variant.buildConfigField("String", "BASE_URL", "\"${baseUrl}\"")

        if (name.contains("driver")) {
            variant.resValue("string", "app_name", "Neptuunia Driver")
        } else if (name.contains("user")) {
            variant.resValue("string", "app_name", "Neptuunia User")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(path: ':data')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'com.google.android.material:material:1.2.1'

    implementation "io.reactivex.rxjava3:rxjava:3.0.0"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"

    implementation "com.google.dagger:hilt-android:2.28-alpha"
    implementation 'com.google.firebase:firebase-config:19.2.0'
    kapt "com.google.dagger:hilt-android-compiler:2.28-alpha"

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation "com.karumi:dexter:5.0.0"

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'

    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
}
